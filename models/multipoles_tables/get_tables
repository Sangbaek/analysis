#!/bin/tcsh

set multipoles_type = 3    # charge channels
set Wmin            = 1080
set Wmax            = 2000
set dW              = 10

# param50 controls running variables. 1=Q2, 2=W
if($1 == "dmt2001")  then # dmt 2001 - param0 selects the type of the multipoles
 set model    = 'maid1?switch=104&param0='
 set default1 = '&param1=1&param2=1&param3=1&param4=1&param7=1&param8=1&param11=1&param12=1&param13=1&param14=1&param15=1&param16=1&param17=1&param18=1&param19=1&param20=1&param21=1'
 set param50  = '&param50=2'
endif

if($1 == "maid2002") then # maid 2002 extended version  - param0 selects the type of the multipoles. Value 11, 12, 13: PS-PV mixing, http://wwwkph.kph.uni-mainz.de/MAID//maidnew/pspvmixing.txt
 set model    = 'maid1?switch=84&param0='
 set default1 = '&param1=1&param2=1&param3=1&param4=1&param7=1&param8=1&param11=1&param12=1&param13=1&param14=1&param15=1&param16=1&param17=1&param18=1&param19=1&param20=1&param21=1&value11=1.0&value12=1.0&value13=1.0'
 set param50  = '&param50=2'
endif

if($1 == "maid2003") then # maid 2003  - param2 selects the type of the multipoles. Param 99 supress output
 set model    = 'maid1?switch=164&param2='
 set default1 = '&param3=1&param4=1&param5=1&param6=1&param9=1&param10=1&param11=1&param12=1&param13=1&param14=1&param15=1&param16=1&param17=1&param18=1&param19=1&param20=1&param21=1&param22=1&param23=1&param24=1&param25=1&param26=1&value11=1.0&value12=1.0&value13=1.0'
 set param50  = '&param50=2&param99=1'
endif

if($1 == "maid2003woroper") then # maid 2003 w/o Roper Resonance  - param2 selects the type of the multipoles - param 15 is the roper. Param 99 supress output
 set model    = 'maid1?switch=164&param2='
 set default1 = '&param3=1&param4=1&param5=1&param6=1&param9=1&param10=1&param11=1&param12=1&param13=1&param14=1&param16=1&param17=1&param18=1&param19=1&param20=1&param21=1&param22=1&param23=1&param24=1&param25=1&param26=1&value11=1.0&value12=1.0&value13=1.0'
 set param50  = '&param50=2&param99=1'
endif

if($1 == "maid2007") then  # maid 2007  - param2 selects the type of the multipoles.
 set model    = 'maid1?switch=214&param2='
 set default1 = '&param3=1&param4=1&param5=1&param6=1&param9=1&param10=1&param11=1&param12=1&param13=1&param14=1&param15=1&param16=1&param17=1&param18=1&param19=1&param20=1&param21=1&param22=1&param23=1&param24=1&param25=1&param26=1&value11=1.0&value12=1.0&value13=1.0'
 set param50  = '&param50=2&param99=1'
endif

mkdir -p $1


set url      = 'http://wwwkph.kph.uni-mainz.de/cgi-bin/'


# ALL values of resonance couplings are set to default value (1.0)
set default2 = '&value51=1.0&value52=1.0&value53=1.0&value54=1.0&value55=1.0&value56=1.0&value57=1.0&value58=1.0&value59=1.0'
set default3 = '&value60=1.0&value61=1.0&value62=1.0&value63=1.0&value64=1.0&value65=1.0&value66=1.0&value67=1.0&value68=1.0&value69=1.0'
set default4 = '&value70=1.0&value71=1.0&value72=1.0&value73=1.0&value74=1.0&value75=1.0&value76=1.0&value77=1.0&value78=1.0&value79=1.0'
set default5 = '&value80=1.0&value81=1.0&value82=1.0&value83=1.0&value84=1.0&value85=1.0&value86=1.0&value87=1.0&value88=1.0&value89=1.0'
set default6 = '&value90=1.0&value91=1.0&value92=1.0&value93=1.0&value94=1.0&value95=1.0&value96=1.0&value97=1.0&value98=1.0&value99=1.0&value100=1.0&value101=1.0&value102=1.0'

if($1 == "maid2002" || $1 == "dmt2001") then
 set default5 = '&value80=1.0&value81=1.0&value82=1.0'
 set default6 = ''
endif


set l = 0

# &value35="$Q2"
# &value36="$Wmin"
# &value41="$dW"
# &value42="$Wmax"

while( $l < 6 )
 set iQ2 = 0
 while( $iQ2 < 5 )
  set dQ2 = 0
  while( $dQ2 < 10)
   set cQ2 = 0
   while ( $cQ2 < 10)

    set Q2 = $iQ2"."$dQ2$cQ2
    echo "Q2 = "$Q2",  l = " $l
    set URL = "$url""$model"$multipoles_type"&value1="$l"$default1$default2$default3$default4$default5$default6$param50""&value35="$Q2"&value36="$Wmin"&value41="$dW"&value42=$Wmax"
    wget "$URL" -O $1/Q2_$Q2"_l_"$l".tbl"


    @ cQ2 += 5
   end
   @ dQ2 += 1
  end
  @ iQ2 += 1
 end

 # last Q2 point is 5
 set Q2 = 5
 echo "Q2 = "$Q2",  l = " $l
 set URL = "$url""$model"$multipoles_type"&value1="$l"$default1$default2$default3$default4$default5$default6$param50""&value35="$Q2"&value36="$Wmin"&value41="$dW"&value42=$Wmax"
 wget "$URL" -O $1/Q2_$Q2"_l_"$l".tbl"

 @ l += 1
end

