#!/bin/csh -f

# Parameters set by user
# ----------------------

set NEVENTS    = 100                             # number of events / job
set NMAXJOBS   = 500                             # number of jobs to be launched per gcard
set GCARDSD    = ~/ddvs                          # collection of gcards. One job / gcard will be launched
set OUTPUTD    = /work/clas/clas12/ungaro/clas12 # output directory
set DEBUG      = no                              # "yes" will print out the jsub file instead of submitting it
set EXPSD      = ~/experiments                   # directory containing the detector definition
set INPUTFILES = none                            # directory containing the input files. Can be "none" for internal generator
                                                 # The files must be of the format "gemcInput_#.dat" where # (file number) will match the job number

# Farm specific parameters
set MEMORY   = "2000 MB"       # node memory to be used
set OS       = centos65        # job OS
set PROJECT  = clas12          # job project name
set MAIL     = ungaro@jlab.org # author email
set TRACK    = simulation      # job track
set JSUBC    = /site/bin/jsub  # command to submit the job

# End of user parameter
# ---------------------


# title of the jobs to be launched
set titles    = `./functions/getGcardsList $GCARDSD`
echo
echo " > Gcards to be launched from " $GCARDSD " :"
echo
echo " >" $titles
echo

set scriptsd  = `pwd`
set i         = 1

# Settings defined here:
# https://scicomp.jlab.org/docs/text_command_file


# Creating jsub and submitting jobs
while ($i <= $NMAXJOBS)

	set w = 1
	while ($w <= $#titles)
		set gcard = $titles[$w]".gcard"
		rm -f gemc.jsub
		\echo  JOBNAME: $titles[$w]                                                    > gemc.jsub
		\echo  PROJECT: $PROJECT                                                      >> gemc.jsub
		\echo  MAIL:    $MAIL                                                         >> gemc.jsub
		\echo  TRACK:   $TRACK                                                        >> gemc.jsub
                \echo  SINGLE_JOB: true                                                       >> gemc.jsub
		\echo  OS:      $OS                                                           >> gemc.jsub
		\echo  MEMORY:  $MEMORY                                                       >> gemc.jsub
		\echo  TIME:    2000                                                          >> gemc.jsub
		\echo  COMMAND: ./goSimulate $titles[$w] $OUTPUTD $NEVENTS $EXPSD $INPUTFILES >> gemc.jsub
		\echo  OTHER_FILES: $scriptsd/goSimulate                                      >> gemc.jsub
		\echo  OTHER_FILES: $scriptsd/functions/logConditions                         >> gemc.jsub
		\echo  OTHER_FILES: $GCARDSD/$gcard                                           >> gemc.jsub
		\echo
		\echo Launching simulation $i for $titles[$w]
		\echo
		if($DEBUG != "yes") then
			$JSUBC gemc.jsub
		else
			cat  gemc.jsub
		endif
		@ w += 1
	end
	@ i += 1 
end

