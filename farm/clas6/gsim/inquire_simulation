#!/bin/csh -f

#./inquire_simulation VERBOSE
#./inquire_simulation copy

# crontab (JLab)
# 45 * * * * ~/projects/analysis/farm/clas6/gsim/inquire_simulation copy > ~/inquireLog


set OPT  = $1

set dir  = /lustre/expphy/volatile/clas/clas12/ungaro/gsim
set dest = /lustre/expphy/volatile/clas/clas12/ungaro/mce16
mkdir -p $dest

set Nevents = 20000  # from input.aao

set N = `ls $dest  | wc | awk '{print $1}'`
@ N += 1

set NEXISTD = `ls $dir | wc | awk '{print $1}'`  # number of existing directories to process

@ NEXISTD += 100

set SIMU = 1

while ($SIMU < $NEXISTD)

	if(`filetest -e  $dir/simu_$SIMU` == "1") then

		# checking for any failure
		set CORE    = (`ls -R $dir/simu_$SIMU | grep core | wc | awk '{print $1}'`)
		if($CORE == "1") then
			echo Core found! Removing dir $dir/simu_$SIMU
			rm -rf $dir/simu_$SIMU   
		endif


		# if mu/log exist, the job has been completed
		if(`filetest -e $dir/simu_$SIMU/mu/log` == "1") then
			if($OPT == "VERBOSE") then
				echo  $dir/simu_$SIMU `grep bos2mu $dir/simu_$SIMU/mu/log | grep Total`
			endif

			if($OPT == "copy") then 

				set DONE1 = `grep Closing $dir/simu_$SIMU/mu/log | wc | awk '{print $1}'`
				set DONE2 = `grep Total   $dir/simu_$SIMU/mu/log | awk -F: '{print $2}'`

				if( $DONE1 == "1") then
					if( $DONE2 == "$Nevents") then
						echo Directory $SIMU is done, with $DONE2 events. Copying to $dest/e16mc_$N.mu to $N and removing it
						mv $dir/simu_$SIMU/mu/e16mc.mu $dest/e16mc_$N.mu
						@ N += 1
						rm -rf $dir/simu_$SIMU
					else
						echo directory $SIMU is done but has only $DONE2 events. Removing it.
						rm -rf $dir/simu_$SIMU
					endif
				endif
			endif
		else
			echo Directory $SIMU in progress. 
		endif
	endif
	@ SIMU += 1
end
 
 
 
