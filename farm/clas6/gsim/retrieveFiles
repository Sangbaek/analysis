#!/bin/csh -f

# This script will copy the remote farm directory on the local disk and rename
# files in order to be ready to be concatenated

# crontab (mauri)
# 15 * * * * /opt/projects/analysis/farm/clas6/gsim/retrieveFiles > /Users/ungaro/retrievedFiles

set remote = /lustre/expphy/volatile/clas/clas12/ungaro/mce16
set localr = /Volumes/e1-6Proc/gsim
set mce16  = $localr/mce16
set dest   = /Volumes/e1-6Proc/gsim/raw
mkdir -p $dest


# checking that mce16 is empty, so it can be removed.
# if not, stop (unless option copyonly is given)
if($1 != "copyonly") then
	if(`ls $mce16 | wc | awk '{print $1}'` != "0") then
		\echo Error: directory $mce16 not empty!
		exit
	endif

	rm -rf $mce16
	scp -r ftp:$remote $localr
endif

# making list of file that can be deleted
rm canBeDeleted.txt ; touch canBeDeleted.txt
cd $mce16
set allFiles = `ls`
cd -
foreach f($allFiles)
	\echo $remote/$f >> canBeDeleted.txt
end
scp canBeDeleted.txt ftp:

# now moving files to raw destination
set N = `\ls $dest | wc | awk '{print $1}'`
foreach f(`\ls $mce16`)
	\echo moving $mce16/$f to $dest/e16mc_$N".mu"
	mv $mce16/$f  $dest/e16mc_$N".mu"
	@ N += 1
end



